function alias-if-in-path {
    # Split on = and extract 1st word (alias name) and 2nd word (alias value)
    local name=${1[(ws:=:)1]}
    local value=${1[(ws:=:)2]}
    # Command is the first word of the alias value
    local cmd=${value[(w)1]}
    # If first command is sudo, the actual command is the second word
    if [[ "$cmd" == "sudo" ]]; then
        cmd=${value[(w)2]}
    fi
    (( $+commands[$cmd] )) && alias $name=$value
}

alias-if-in-path aptup='sudo aptitude update && sudo aptitude safe-upgrade'
alias-if-in-path autossh='autossh -M 0 -o "ServerAliveInterval 10"'
alias-if-in-path diff='colordiff'
alias-if-in-path ec='emacsclient -nq'
alias-if-in-path v='vagrant ssh'

alias git-root='cd $(git rev-parse --show-toplevel)'
alias week='date +%V'
alias zrehash='source ~/.zshrc'

# prezto helpers
alias prezto-update='cd ~/.zprezto && git pull --rebase && \
    git submodule update --init --recursive && cd -'
alias prezto-diff='diff -u ~/.zprezto/runcoms/zpreztorc ~/.zpreztorc | less -R'

# Clean up functions
unfunction alias-if-in-path

case "$OSTYPE" in
    darwin*)
        if (( $+commands[gls] )); then
            alias ls='gls --group-directories-first --color=auto'
        else
            alias ls='ls -G'
        fi
        alias pgrep='pgrep -f -l -i'
        ;;
    freebsd*)
        if (( $+commands[gnuls] )); then
            alias ls='gnuls --color=auto'
        else
            alias ls='ls -G'
        fi
        alias pgrep='pgrep -f -l -i'
        ;;
    *)
        alias ls='ls --group-directories-first --color=auto'
        alias pgrep='pgrep -f -l'
esac

function ssh-rm-host-key {
    if [[ "$1" =~ ^[0-9]+$ ]]; then
        case "$OSTYPE" in
            darwin* | freebsd*)
                sed -i '' -e "${1}d" ~/.ssh/known_hosts
                ;;
            *)
                sed -i'' -e "${1}d" ~/.ssh/known_hosts
        esac
    else
        echo "usage: $0 line-number"
    fi
}

function tmux-rehash-panes {
    if (( ! $+commands[tmux] )); then
        return 1
    fi
    local panes
    panes=$(tmux list-panes -aF \
        '#{session_name}:#{window_index}.#{pane_index},#{pane_pid}')
    local line
    for line in ${(f)panes}; do
        local pane=${line[(ws:,:)1]}
        local pane_pid=${line[(ws:,:)2]}
        # If pane_pid is NOT the parent of any process, then it's most like a
        # interactive shell and we can send our command
        if ! command pgrep -P $pane_pid > /dev/null; then
            command tmux send-keys -t $pane 'source ~/.zshrc' enter
        fi
    done
}
