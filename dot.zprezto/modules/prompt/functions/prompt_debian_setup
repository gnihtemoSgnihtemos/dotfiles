function prompt_debian_precmd {
  vcs_info

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_debian_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz colors && colors
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_debian_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' formats ' %F{red}%b%c%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '[%v]'

  # Only show user@hostname if connected through SSH
  if [[ -n "$SSH_TTY" ]]; then
    PROMPT_HOST='%{$fg_bold[green]%}%n@%m%{$reset_color%}:'
  else
    PROMPT_HOST=''
  fi

  # Define prompts.
  PROMPT="$PROMPT_HOST"'%{$fg_bold[blue]%}%~${vcs_info_msg_0_}%{$reset_color%}\$ '
  RPROMPT='$python_info[virtualenv]'
}

prompt_debian_setup "$@"

