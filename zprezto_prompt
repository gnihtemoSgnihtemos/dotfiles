function prompt_debian_precmd {
  vcs_info

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_debian_symbol {
  zmodload zsh/datetime
  local symbol=''
  local -r burger=$(printf "\xF0\x9F\x8D\x94")
  local -r coffee=$(printf "\xE2\x98\x95")
  local -r beer=$(printf "\xF0\x9F\x8D\xBA")
  local -r hour=$(strftime %-k $EPOCHSECONDS)
  if (( $hour >= 8 && $hour < 16 )); then
    symbol=$coffee
  elif (( $hour >= 16 && $hour < 19 )); then
    symbol=$burger
  else
    symbol=$beer
  fi
  printf $symbol
}

function prompt_debian_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz colors && colors
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_debian_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' formats ' %F{red}%b%c%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '[%v]'

  # Show user@hostname prefix if connected through SSH
  local host_prefix=''
  if [[ -n "$SSH_TTY" ]]; then
    host_prefix='%{$fg_bold[green]%}%n@%m%{$reset_color%}:'
  fi

  # Display fancy symbol on darwin
  local symbol=''
  if [[ "$OSTYPE" == darwin* ]]; then
    symbol="$(prompt_debian_symbol)  "
  else
    symbol='$ '
  fi

  # Define prompts.
  PROMPT="$host_prefix"'%{$fg_bold[blue]%}%~${vcs_info_msg_0_}%{$reset_color%}'"$symbol"
  RPROMPT='$python_info[virtualenv]'
}

prompt_debian_setup "$@"
